apply plugin: 'java'

sourceSets {
    main {
        resources {
            srcDirs 'src/main/bash'
        }
    }
}

Closure isCommandAvailable = { String cmd ->
    try {
        cmd.execute()
        return true
    } catch (IOException e) {
    }
    return false
}

task installShellcheckIfMissing(type: Exec) {
    boolean shellcheckInstalled = isCommandAvailable("shellcheck -V")
    if (shellcheckInstalled) {
        commandLine "echo", "Shellcheck installed - continuing"
    } else {
        println "Shellcheck is missing. Will install it."
        commandLine "bash", new File(project.rootDir, "tools/build-helper.sh").path, "download-shellcheck"
    }
}

task installBatsIfMissing(type: Exec) {
    boolean batsInstalled = isCommandAvailable("bats -v")
    if (batsInstalled) {
        commandLine "echo", "Bats installed - continuing"
    } else {
        println "Bats is missing. Will try to install it."
        commandLine "bash", new File(project.rootDir, "tools/build-helper.sh").path, "download-bats"
    }
}

task shellcheck {
    doLast {
        boolean shellcheckInstalled = isCommandAvailable("shellcheck -V")
        String pathToShellcheck = "shellcheck"
        if (!shellcheckInstalled) {
            pathToShellcheck = new File(project(":common").buildDir, "shellcheck-latest/shellcheck").path
        }
        sourceSets*.resources.srcDirs*.each { srcDir ->
            fileTree(relativePath(srcDir)).include('**/*.sh').each { File script ->
                exec {
                    commandLine pathToShellcheck, script
                }
            }
        }
    }
}

task bats {
    doLast {
        boolean batsInstalled = isCommandAvailable("bats -v")
        String pathToBats = "bats"
        String outputTapFile = project(":common").buildDir.path + "/bats.tap"
        if (!batsInstalled) {
            pathToBats = new File(project(":common").buildDir, "bats/bin/bats").path
        }
        exec {
            standardOutput = new org.apache.tools.ant.util.TeeOutputStream(
                new FileOutputStream(outputTapFile), System.out);
            commandLine pathToBats, '-t', 'src/test/bats'
        }
    }
}

shellcheck.dependsOn("installShellcheckIfMissing")
build.dependsOn("shellcheck")
bats.dependsOn("installBatsIfMissing")
build.dependsOn("bats")
